/*
 * Copyright 2015 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "build-time-tracker"

// query git for the SHA, Tag and commit count. Use these to automate versioning.
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 100 +
        Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "io.plaidapp"
        minSdkVersion 21
        targetSdkVersion 25
        multiDexEnabled true
        versionCode gitCommitCount
        versionName gitTag
        archivesBaseName = "plaid"
        resConfig "en"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "DRIBBBLE_CLIENT_ID", "\"${dribbble_client_id}\""
        buildConfigField "String", "DRIBBBLE_CLIENT_SECRET", "\"${dribbble_client_secret}\""
        buildConfigField "String",
                "DRIBBBLE_CLIENT_ACCESS_TOKEN", "\"${dribbble_client_access_token}\""
        buildConfigField "String", "DESIGNER_NEWS_CLIENT_ID", "\"${designer_news_client_id}\""
        buildConfigField "String",
                "DESIGNER_NEWS_CLIENT_SECRET", "\"${designer_news_client_secret}\""
        buildConfigField "String",
                "PROCUCT_HUNT_DEVELOPER_TOKEN", "\"${product_hunt_developer_token}\""
        def filesAuthorityValue = applicationId + ".shareprovider";
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}



sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

buildtimetracker {
    reporters {
        csv {
            output "times.csv"
            append true
            header false
        }

        summary {
            ordered true
            threshold 50
            barstyle "unicode"
        }

        csvSummary {
            csv "times.csv"
        }
    }
}

tasks.whenTaskAdded { task -> if (task.name == "compileDebugGroovyWithGroovyc") { task.enabled = false } }
tasks.whenTaskAdded { task -> if (task.name == "compileQaGroovyWithGroovyc") { task.enabled = false } }
tasks.whenTaskAdded { task -> if (task.name == "compileReleaseGroovyWithGroovyc") { task.enabled = false } }

repositories {
    jcenter()
}

ext.supportLibVersion = '25.3.1'
ext.retrofit2Version = '2.2.0'

dependencies {
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:customtabs:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:palette-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    compile 'com.google.code.gson:gson:2.8.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    compile 'com.jakewharton:butterknife:8.6.0'
    compile "com.squareup.retrofit2:retrofit:${retrofit2Version}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit2Version}"
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'org.jsoup:jsoup:1.10.1'

    compile 'com.github.InkApplications:android-layout-injector:1.1.0'
    compile 'com.github.InkApplications:android-application-lifecycle:1.0.1'
    compile 'com.github.InkApplications:android-logger:4.1.0'
    compile 'com.github.InkApplications:guava-stopwatch:21.0'
    compile 'com.github.kylealanr:GestureDetectors:1.1'

    compile project(':bypass')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

apply plugin: 'me.tatarka.retrolambda'